apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'

android {
    namespace 'ch.derlin.easypass.easypass'
    defaultConfig {
        applicationId "ch.derlin.easypass"
        minSdkVersion 33
        compileSdk 34
        targetSdkVersion 34
        versionCode 3
        versionName "2.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        // see https://stackoverflow.com/questions/46947008/updated-android-studio-to-3-0-and-getting-this-issue
        vectorDrawables.useSupportLibrary = true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
}


dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('androidx.test.espresso:espresso-core:3.5.1', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation files('libs/not-yet-commons-ssl-0.3.11.jar')
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.dropbox.core:dropbox-core-sdk:7.0.0'
    implementation 'com.dropbox.core:dropbox-android-sdk:7.0.0'
    implementation 'com.google.code.gson:gson:2.11.0'
    api 'com.google.android.material:material:1.12.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.2'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'nl.komponents.kovenant:kovenant-android:3.3.0'
    implementation 'nl.komponents.kovenant:kovenant-ui:3.3.0'
    implementation 'io.github.ShawnLin013:number-picker:2.4.13'
    implementation 'com.github.apl-devs:appintro:v4.2.2'
    implementation 'ch.derlin.android:changelog:1.1@aar'
    testImplementation 'junit:junit:4.13.2'
}

// github tokens for access to Github Packages
def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))

repositories {
    mavenCentral()
    maven { // see https://github.com/derlin/android-changelog
        name = "GitHubPackages"
        url = uri("https://maven.pkg.github.com/derlin/android-changelog")

        credentials {
            username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
            password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
        }
    }
}
